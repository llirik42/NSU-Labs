import expression;
import ds/tree;
import ds/set;

export {
    SimplificationRule : (from: Exp, to: Exp);

    getRuleRepr(rule: SimplificationRule) -> string;

    tryToApplyRule(e: Exp, rule: SimplificationRule) -> Maybe<Exp>;
}

isRawReplacementTreeCorrect(rawReplacementTree: Tree<string, [Exp]>) -> bool {
    foldTree(rawReplacementTree, true, \k, v, acc -> length(v) == 1 && acc);
}

replaceVars(e: Exp, replacementTree: Tree<string, Exp>) -> Exp {
    switch(e) {
        // It's guaranteed that replacementTree contains value for every variable in e, so defaultValue (third argument) in lookupTreeDef() can be any
        Var(name): lookupTreeDef(replacementTree, name, e); 

        Neg(e1): Neg(replaceVars(e1, replacementTree));

        Binary(l, r, op): Binary(replaceVars(l, replacementTree), replaceVars(r, replacementTree), op);
    
        default: e;
    }
}

/* 
    Merges two array-trees and keeps resulting arrays unique.
    Example: 
    
    t1 = TreeNode(1, [2, 3], TreeEmpty(), TreeEmpty(), 1)
    t2 = TreeNode(1, [2, 10], TreeEmpty(), TreeEmpty(), 1)
    
    then
    mergeTreeArrayUnique(t1, t2) = TreeNode(1, [2, 3, 10], TreeEmpty(), TreeEmpty(), 1)

    Note: mergeTree(t1, t2) = TreeNode(1, [2, 10], TreeEmpty(), TreeEmpty(), 1)
    So t2 overrides t1 on duplicate entries.
*/
mergeTreeArrayUnique(t1 : Tree<?, [??]>, t2 : Tree<?, [??]>) -> Tree<?, [??]> {
    mergeTreeCustom(t1, t2, \k, a1, a2 -> {
        set2array(buildSet(concat(a1, a2)));
    })
}

getRuleRepr(rule: SimplificationRule) -> string {
    getRepr(rule.from) + " -> " + getRepr(rule.to);
}

/*
    Matches expression and part of rule.
    Return value: 
        1) None() if e and ruleExp have different structure
        2) Tree, where keys = names of variables of ruleExp and values = arrays of expressions (from e) that match variable with the name

    Examples:
        1) e = 1 + 2, ruleExp = a + b, then {"a": [1], "b": [2]}
        2) e = 1 + 2 + 3, ruleExp = a + b + a, then {"a": [1, 3], "b": [2]}

*/
match(e: Exp, ruleFrom: Exp, matches: Tree<string, [Exp]>) -> Maybe<Tree<string, [Exp]>> {
    switch(ruleFrom) {
        Var(name): Some(treePushToArrayUnique(matches, name, e));

        Neg(e2): {
            switch(e) {
                Neg(e1): match(e1, e2, matches);
                default: None();
            }
        }
        
        Binary(l2, r2, op2): {
            switch(e) {
                Binary(l1, r1, op1): {
                    if (op1 == op2) {
                        m1 = match(l1, l2, matches);
                        m2 = match(r1, r2, matches);
                        maybeMap2(mergeTreeArrayUnique)(m1, m2);
                    } else {
                        None();
                    }
                }
                default: None();
            }
        }

        default: if (e == ruleFrom) Some(matches) else None();
    }
}

tryToApplyRule(e: Exp, rule: SimplificationRule) -> Maybe<Exp> {
    matches = match(e, rule.from, makeTree());

    // Whether we can apply rule or not
    if (eitherMap(matches, isRawReplacementTreeCorrect, false)) {
        matchesTree: Tree<string, [Exp]> = either(matches, makeTree());
        replacementTree = mapTree(matchesTree, \a -> a[0]);
        Some(replaceVars(rule.to, replacementTree));
    } else {
        None();
    }
}

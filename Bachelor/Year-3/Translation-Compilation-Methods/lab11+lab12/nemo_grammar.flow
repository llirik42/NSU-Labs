import string;
import ds/tree;

export {    
    // Variable types
    Int : ();
    String : ();
    Bool : ();
    Real : ();
    NoType : ();
    AnyType : ();
    Array : (baseType : ExprType);
    VarType ::= Int, String, Bool, Real, Array;
    ExprType ::= VarType, NoType, AnyType;

    // Declaration of variables
    VarDecl : (name : string, type : VarType);
    VarDeclList : (elements : [VarDecl]);
    varDeclList2s(vdl : VarDeclList) -> string;

    // Variables
    Var : (name : string);
    
    // Constants
    IntConst : (value: int);
    StringConst : (value : string);
    BoolConst : (value : bool);
    RealConst : (value : double);
    ArrayConst : (values : Tree<int, Const>); // So we can execute for example UPD([], 10, 10).
    NonBoolConst ::= IntConst, StringConst, RealConst, ArrayConst;
    Const ::= NonBoolConst, BoolConst;
    const2s(c : Const) -> string;

    // Binary arithmetic operations
    ArithmAddOp : ();
    ArithmSubOp : ();
    ArithmMulOp : ();
    ArithmDivOp : ();
    ArithmBinaryOp ::= ArithmAddOp, ArithmSubOp, ArithmMulOp, ArithmDivOp;
    
    // Arithmetic expressions
    ArithmBinary : (l : Expr, r : Expr, op : ArithmBinaryOp);
    ArithmNeg : (e : Expr);
    ArithmExpr ::= ArithmBinary, ArithmNeg;

    // Boolean expressions
    Equal : ();
    Less : ();
    ComparisonType ::= Equal, Less;
    Comparison : (l : Expr, r : Expr, comparisonType : ComparisonType);

    Not : (e : BooleanExpr);
    Or : (l : BooleanExpr, r : BooleanExpr);
    
    BooleanExpr ::= Comparison, Not, Or, BoolConst;
    
    // Array operations
    ArrayUpdateOpType : (ind : Expr, val : Expr);
    ArrayApplyOpType : (ind : Expr);
    ArrayConcatOpType : (arr2 : Expr);
    ArrayLengthOpType : ();
    ArrayOpType ::= ArrayUpdateOpType, ArrayApplyOpType, ArrayConcatOpType, ArrayLengthOpType;
    ArrayOp : (arr : Expr, opType : ArrayOpType);

    // Expressions
    Expr ::= Var, Const, ArithmExpr, ArrayOp, BooleanExpr;
    expr2s(e : Expr) -> string;

    Assignment : (varName : string, expr : Expr);
    BodiesSeq : (bodies : [Body]);
    Test : (be : BooleanExpr);
    Print : (e : Expr);
    UDChoice : (bodies : [Body]);
    UDIteration : (body : Body);
    Body ::= Assignment, BodiesSeq, Test, UDChoice, UDIteration, Print;

    NemoProgram : (varDeclList : VarDeclList, body : Body);
}

// Utils
doRange(len : int, acc : [int], index : int) -> [int] {
    if (index == len) {
        acc;
    } else {
        doRange(len, arrayPush(acc, index), index + 1);
    }
}

range(maxIndex : int) -> [int] {
    doRange(maxIndex + 1, [], 0);
}


constTree2s(t : Tree<int, Const>) -> string {
    switch (popmax(t)) {
        // Tree is empty
        EmptyPopResult() : "[]";
        
        // Tree is non-empty
        PopResult(resK, resV, rest) : {
            maxIndex = resK;
            r = range(maxIndex);
            l = foldi(
                range(maxIndex),
                "",
                \i, acc, e -> {
                    curConst = lookupTree(t, e);
                    curConstRepr = eitherMap(curConst, const2s, "__");
                    
                    tmp = acc + curConstRepr;

                    if (i == maxIndex) {
                        tmp;
                    } else {
                        tmp + ", ";
                    }
                }
            );

            "[" + l + "]";
        }
    };
}


exprType2s(t : ExprType) -> string {
    switch (t) {
        Int() : "INT";
        String() : "STR";
        Bool() : "BOOL";
        Real() : "REAL";
        Array(baseType) : "[" + exprType2s(baseType) + "]";
        NoType() : "<No type>";
        AnyType() : "<Any type>";
    }
}

varDecl2s(vd: VarDecl) -> string {
    "VAR " + vd.name + " : " + exprType2s(vd.type) + ";";
}

varDeclList2s(vdl : VarDeclList) -> string {
    fold(vdl.elements, "", \s, d -> {
        s + varDecl2s(d) + "\n";
    })
}

boolConst2s(b : BoolConst) -> string {
    b2s(b.value);
}

const2s(c : Const) -> string {
    switch (c) {
        IntConst(value) : i2s(value);
        StringConst(value) : "\"" + value + "\"";
        BoolConst(value) : boolConst2s(c);
        RealConst(value) : d2s(value);
        ArrayConst(values) : constTree2s(values);
    }
}

arithmExpr2s(ae : ArithmExpr) -> string {
    withoutBrackets = switch (ae) {
        ArithmBinary(l, r, op) : {
            ls = expr2s(l);
            rs = expr2s(r);
            
            switch (op) {
                ArithmAddOp() : ls + " + " + rs;
                ArithmSubOp() : ls + " - " + rs;
                ArithmMulOp() : ls + " * " + rs;
                ArithmDivOp() : ls + " / " + rs;
            }
        }
        
        ArithmNeg(e) : "-" + expr2s(e);
    }

    "(" + withoutBrackets + ")";
}

arrayOp2s(arrayOp : ArrayOp) -> string {
    arr = arrayOp.arr;
    arrRepr = expr2s(arr);

    switch (arrayOp.opType) {
        ArrayUpdateOpType(ind, val) : "UPD(" + arrRepr + ", " + expr2s(ind) + ", " + expr2s(val) + ")";

        ArrayApplyOpType(ind) : "APP(" + arrRepr + ", " + expr2s(ind) + ")";

        ArrayConcatOpType(arr2) : "CONCAT(" + arrRepr + ", " + expr2s(arr2) + ")";

        ArrayLengthOpType() : "LENGTH(" + arrRepr + ")";
    }
}

comparisonType2s(ct : ComparisonType) -> string {
    switch (ct) {
        Equal() : " = ";
        Less() : " < ";
    }
}

booleanExpr2s(be : BooleanExpr) -> string {
    switch (be) {
        Comparison(l, r, ct) : expr2s(l) + comparisonType2s(ct) + expr2s(r);
        Not(e) : "NOT(" + booleanExpr2s(e) + ")";
        Or(l, r) : "(" + booleanExpr2s(l) + ")" + " OR " + "(" + booleanExpr2s(r) + ")";
        BoolConst(value) : boolConst2s(be);
    }
}

expr2s(e : Expr) -> string {
    switch (e) {
        Var(name) : name;
        NonBoolConst() : const2s(e);
        ArithmExpr() : arithmExpr2s(e);
        ArrayOp(arr, opType) : arrayOp2s(e);
        BooleanExpr() : booleanExpr2s(e);
    }
}
